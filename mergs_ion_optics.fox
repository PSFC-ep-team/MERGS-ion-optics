INCLUDE 'COSY';
PROCEDURE mergs_ion_optics;

VARIABLE central_energy 1;

VARIABLE foil_width 1;
VARIABLE foil_height 1;
VARIABLE aperture_width 1;
VARIABLE aperture_height 1;

VARIABLE shape_in 1 3;
VARIABLE p_shape_in_1 1;
VARIABLE p_shape_in_2 1;
VARIABLE p_shape_in_3 1;
VARIABLE shape_out 1 3;
VARIABLE p_shape_out_1 1;
VARIABLE p_shape_out_2 1;
VARIABLE p_shape_out_3 1;
VARIABLE shape_zero 1 3;

VARIABLE p_bend_radius 1;
VARIABLE p_bend_angle 1;
VARIABLE p_drift_pre_aperture 1;
VARIABLE p_drift_post_aperture 1;
VARIABLE p_drift_m5a_m5b 1;
VARIABLE p_drift_pre_bend 1;
VARIABLE p_drift_post_bend 1;
VARIABLE p_drift_pre_hodoscope 1;
VARIABLE p_m5a_length 1;
VARIABLE p_m5a_quad 1;
VARIABLE p_m5a_hex 1;
VARIABLE p_m5b_length 1;
VARIABLE p_m5b_quad 1;
VARIABLE p_m5b_hex 1;
VARIABLE p_m5c_length 1;
VARIABLE p_m5c_quad 1;
VARIABLE p_m5c_hex 1;
VARIABLE p_m5d_length 1;
VARIABLE p_m5d_quad 1;
VARIABLE p_m5d_hex 1;

VARIABLE input_rays 1 1000 8;
VARIABLE output_rays 1 1000 8;
VARIABLE num_rays 1;

VARIABLE resolution 1;
VARIABLE focal_plane_tilt 1;
VARIABLE focal_plane_bend 1;
VARIABLE focal_plane_length 1;
VARIABLE focal_plane_height 1;

{============ UTILITY FUNCTIONS ============}

{
    Create a new ray to be traced.
    - x and y are position in meters.
    - a and b are indicate x and y components of normalized momentum. ({a,b} = p_{x,y}/abs(p))
    - t is time of flight offset
    - energy, mass, and charge are relative.
}
PROCEDURE SET_RAY x a y b t energy mass charge color;
    SR x a y b t energy mass charge color;
    num_rays := num_rays + 1;
    input_rays(num_rays,1) := x;
    input_rays(num_rays,2) := a;
    input_rays(num_rays,3) := y;
    input_rays(num_rays,4) := b;
    input_rays(num_rays,5) := t;
    input_rays(num_rays,6) := energy;
    input_rays(num_rays,7) := mass;
    input_rays(num_rays,8) := charge;
ENDPROCEDURE;

{ Clear the rays currently in the system. }
PROCEDURE CLEAR_RAYS;
    CR; {clear rays}
    num_rays := 0;
ENDPROCEDURE;

{ Unity map / Set the transfer map to the identity. }
PROCEDURE UNITY_MAP;
    UM; {uninitialize map}
ENDPROCEDURE;

{ Create an aperture element with given radius. }
PROCEDURE ELT_APERTURE radius;
    PS radius; {poincare surface (aperture)}
ENDPROCEDURE;
{ Create an element with no field and given length. }
PROCEDURE ELT_DRIFT_LENGTH length;
    DL length; {drift length}
ENDPROCEDURE;
{ Create a fifth order multipole magnet. }
PROCEDURE ELT_MULTIPOLE_5 length b_quad b_hex b_oct b_dec b_dodec bore;
    M5 length b_quad b_hex b_oct b_dec b_dodec bore;
ENDPROCEDURE;
{
    Create a combined function bending magnet with given radius and
    arc angle.
        - `aperture` sets the size of the beam-ok region,
        - `shape_in` and `shape_out` are coefficinets for polynomials that
        shape the entrance and exit in the beam "z" direction. [these
        arrays have length `len`]
}
PROCEDURE ELT_BENDING radius angle aperture N shape_in shape_out len;
    MC radius angle aperture N shape_in shape_out len; {makes a bending magnet}
ENDPROCEDURE;

{ Set the fringe fields to the most accurate version. }
PROCEDURE CONFIG_FRINGE_FIELD_MODE_ACCURATE;
    FR 3; {fringe mode = accurate}
ENDPROCEDURE;
{
    - `order`: maximum order of DA transfer map.
    - `phase_space_dim`: 1: x/a only, 2: add y/b, 3: add time/chromatic effects.
    - `parameters`: number of additional parameters to include
}
PROCEDURE CONFIG_ORDER_AND_VARIABLES order phase_space_dim n_parameters;
    OV order phase_space_dim n_parameters;
ENDPROCEDURE;

{ 
    Tell COSY that we're working with an electron beam.
    must be called after CONFIG_ORDER_AND_VARIABLES. :P
}
PROCEDURE CONFIG_AS_ELECTRON_BEAM nominal_energy;
    RPE nominal_energy; {become electron with}
ENDPROCEDURE;

PROCEDURE DRAW_BEGIN_PATH;
    BP; {begin path (drawing)}
ENDPROCEDURE;
PROCEDURE DRAW_END_PATH;
    EP; {end path (drawing)}
ENDPROCEDURE;

PROCEDURE WRITE_MAP destination;
    PM destination; {print map}
ENDPROCEDURE;

{============ MATH FUNCTIONS ============}

{ fill out output_rays based on input_rays and the current map }
PROCEDURE transform_rays;
	VARIABLE i_ray 1;
	VARIABLE i_coord 1;
	VARIABLE input_ray 1 8;
	VARIABLE output_ray 1 8;
	VARIABLE map 10000 8;

    { save the map to a variable }
	SM map;

	LOOP i_ray 1 num_rays;
		LOOP i_coord 1 8;
			input_ray(i_coord):=input_rays(i_ray,i_coord);
		ENDLOOP;
		POLVAL 1 map TWOND input_ray TWOND output_ray TWOND ;
		LOOP i_coord 1 8;
			output_rays(i_ray,i_coord):=output_ray(i_coord);
		ENDLOOP;
	ENDLOOP; 
ENDPROCEDURE;

{ determine the distance from the last element to the point of best first-order focus }
PROCEDURE calculate_final_drift;
    VARIABLE first_derivative 1;
    VARIABLE twoth_derivative 1;
    VARIABLE i 1;

    transform_rays;

    first_derivative := 0;
    twoth_derivative := 0;
    LOOP i 1 num_rays;
        IF input_rays(i,6)=0;
            first_derivative := first_derivative + output_rays(i,1)*output_rays(i,2);
            twoth_derivative := twoth_derivative + output_rays(i,2)^2;
        ENDIF;
    ENDLOOP;

    p_drift_pre_hodoscope := -first_derivative/twoth_derivative;
    IF p_drift_pre_hodoscope<0;
        p_drift_pre_hodoscope := 0;
    ENDIF;
ENDPROCEDURE;

{ calculate key performance metrics of the map }
PROCEDURE evaluate_spectrometer;
    VARIABLE sum_x_squared 1;
    VARIABLE count 1;
    VARIABLE i 1;
    VARIABLE numerator 1;
    VARIABLE denominator 1;
    VARIABLE min_x 1;
    VARIABLE max_x 1;
    VARIABLE min_y 1;
    VARIABLE max_y 1;

    transform_rays;

    sum_x_squared := 0;
    count := 0;
    LOOP i 1 num_rays;
        IF input_rays(i,6)=0;
            sum_x_squared := sum_x_squared + output_rays(i,1)^2/num_rays; {m^2}
            count := count + 1;
        ENDIF;
    ENDLOOP;
    resolution := SQRT(sum_x_squared/count)/ME(1,6)*2.355; {(relative)}

    focal_plane_tilt := ATAN(-ME(1,26)/ME(2,2)/ME(1,6)); {rad}

    numerator := ME(1,1)*(ME(1,6)*ME(1,266)-ME(1,26)*(ME(1,66)+2*ME(1,6)*ME(1,1)*ME(2,26)));
    denominator := (ME(1,6)^2+ME(1,1)^2*ME(1,26)^2)^1.5;
    focal_plane_bend := numerator/denominator; {m^-1}

    min_x := 1e10;
    max_x := -1e10;
    min_y := 1e10;
    max_y := -1e10;
    LOOP i 1 num_rays;
        { for y, look at all ray energies }
        IF output_rays(i,3)<min_y;
            min_y := output_rays(i,3);
        ENDIF;
        IF output_rays(i,3)>max_y;
            max_y := output_rays(i,3);
        ENDIF;
        { for x, look at only central rays }
        IF (input_rays(i,1)=0)*(input_rays(i,2)=0)*(input_rays(i,3)=0)*(input_rays(i,4)=0);
            IF output_rays(i,1)<min_x;
                min_x := output_rays(i,1);
            ENDIF;
            IF output_rays(i,1)>max_x;
                max_x := output_rays(i,1);
            ENDIF;
        ENDIF;
    ENDLOOP;
    focal_plane_length := max_x - min_x; {m}
    focal_plane_height := max_y - min_y; {m}
 ENDPROCEDURE;

{============ CORE FUNCTIONS ============}

PROCEDURE gen_rays energy color;
    VARIABLE x_foil 1;
    VARIABLE y_foil 1;
    VARIABLE x_aperture 1;
    VARIABLE y_aperture 1;
    LOOP x_foil -foil_width/2 foil_width/2 foil_width/2;
        LOOP y_foil -foil_height/2 foil_height/2 foil_height/2;
            LOOP x_aperture -aperture_width/2 aperture_width/2 aperture_width/2;
                LOOP y_aperture -aperture_height/2 aperture_height/2 aperture_height/2;
                    SET_RAY x_foil (x_aperture-x_foil)/p_drift_pre_aperture
                            y_foil (y_aperture-y_foil)/p_drift_pre_aperture
                            0 energy 0 0 color;
                ENDLOOP;
            ENDLOOP;
        ENDLOOP;
    ENDLOOP;
ENDPROCEDURE;

PROCEDURE config_spectrometer;
    CONFIG_ORDER_AND_VARIABLES 3 3 0; {3rd-order calculation, account for chromatic effects, no "knobs"}
    central_energy := 16; {MeV}
    CONFIG_AS_ELECTRON_BEAM central_energy;
ENDPROCEDURE;

PROCEDURE elt_spectrometer;
    shape_in(1) := p_shape_in_1;
    shape_in(2) := p_shape_in_2;
    shape_in(3) := p_shape_in_3;
    shape_out(1) := p_shape_out_1;
    shape_out(2) := p_shape_out_2;
    shape_out(3) := p_shape_out_3;
    shape_zero(1) := 0;
    shape_zero(2) := 0;
    shape_zero(3) := 0;

    UNITY_MAP;

    ELT_DRIFT_LENGTH p_drift_pre_aperture;

    ELT_APERTURE MAX(aperture_width, aperture_height)/2;

    ELT_DRIFT_LENGTH p_drift_post_aperture;

    ELT_MULTIPOLE_5
        p_m5a_length
        p_m5a_quad p_m5a_hex 0 0 0
        0.05;

    ELT_DRIFT_LENGTH p_drift_m5a_m5b;

    ELT_MULTIPOLE_5 
        p_m5b_length
        p_m5b_quad p_m5b_hex 0 0 0
        0.05;

    ELT_DRIFT_LENGTH p_drift_pre_bend;

    ELT_BENDING
        p_bend_radius p_bend_angle 0.075
        shape_zero shape_in shape_out 3;

    ELT_DRIFT_LENGTH p_drift_post_bend;

    ELT_MULTIPOLE_5 
        p_m5c_length
        p_m5c_quad p_m5c_hex 0 0 0
        0.05;

    calculate_final_drift;
    ELT_DRIFT_LENGTH p_drift_pre_hodoscope;
    
    ELT_APERTURE 0.05; {for scoping hodoscope size}
ENDPROCEDURE;

PROCEDURE main;
    VARIABLE output_mode 1;
    VARIABLE out 1;
    VARIABLE y_scale 1;
    VARIABLE objective 1;
    VARIABLE optimizer 1;

    { set the output mode (0: command line, 1: GUI, 2: direct-to-PDF) }
    output_mode := 1; {{CONTROL}}

    { ---- input parameters ---- }

    config_spectrometer;

    { set upper bound for the foil dimensions }
    foil_width := 0.02;
    foil_height := 0.04;
    aperture_width := 0.02;
    aperture_height := 0.04;

    { set the ion-optic parameters }
    p_bend_radius := 0.53; {{PARAM |min=0.02 |max=inf |bias=0 |unit=m}}
    p_bend_angle := 110; {{PARAM |min=0 |max=120 |bias=-1/360 |unit=deg}}
    p_drift_pre_aperture := 0.20;
    p_drift_post_aperture := 0.05; {{PARAM |min=0 |max=1 |bias=-1/1.00 |unit=m}}
    p_drift_m5a_m5b := 0.08; {{PARAM |min=0 |max=1 |bias=-1/1.00 |unit=m}}
    p_drift_pre_bend := 0.15; {{PARAM |min=0 |max=1 |bias=-1/1.00 |unit=m}}
    p_drift_post_bend := 0.20; {{PARAM |min=0 |max=1 |bias=-1/1.00 |unit=m}}
    p_drift_pre_hodoscope := 0; {{CONSTRAINT |min=0 |max=1 |bias=-1/1.00 |unit=m}}
    p_m5a_length := 0.10; {{PARAM |min=0 |max=1 |bias=-1/0.10 |unit=m}}
    p_m5a_quad := -0.062; {{PARAM |min=0 |max=0.2 |bias=-1/0.10 |unit=T}}
    p_m5a_hex := 0; {{PARAM |min=0 |max=0.2 |bias=-1/0.10 |unit=T}}
    p_m5b_length := 0.10; {{PARAM |min=0 |max=1 |bias=-1/0.10 |unit=m}}
    p_m5b_quad := 0.115; {{PARAM |min=0 |max=0.2 |bias=-1/0.10 |unit=T}}
    p_m5b_hex := 0; {{PARAM |min=0 |max=0.2 |bias=-1/0.10 |unit=T}}
    p_shape_in_1 := 0.2; {{PARAM |min=-1 |max=1 |bias=0 |unit=rad}}
    p_shape_in_2 := 0; {{PARAM |min=-20 |max=20 |bias=-1/10 |unit=1/m}}
    p_shape_in_3 := 0; {{PARAM |min=-500 |max=500 |bias=-1/200 |unit=1/m^2}}
    p_shape_out_1 := 0.2; {{PARAM |min=-1 |max=1 |bias=0 |unit=rad}}
    p_shape_out_2 := 0; {{PARAM |min=-20 |max=20 |bias=-1/10 |unit=1/m^2}}
    p_shape_out_3 := 0; {{PARAM |min=-500 |max=500 |bias=-1/200 |unit=1/m^3}}
    p_m5c_length := 0.10; {{PARAM |min=0 |max=1 |bias=-1/0.10 |unit=m}}
    p_m5c_quad := -0.016; {{PARAM |min=0 |max=0.2 |bias=-1/0.10 |unit=T}}
    p_m5c_hex := 0; {{CONSTRAINT |min=0 |max=0.2 |bias=-1/0.10 |unit=T}}

    { ---- final hexapole optimization ---- }

    { optimization algorithms
        1 - Nelder-Mead
        3 - simulated annealing
        4 - Levenberg-Marquardt }
    optimizer := 4;

    y_scale := 1;

    FIT p_m5c_hex;
        UNITY_MAP;
        CLEAR_RAYS;
        gen_rays 0 1;

        IF output_mode=1;
            PTY y_scale;
            DRAW_BEGIN_PATH;
        ENDIF;
        elt_spectrometer;
        IF output_mode=1;
            DRAW_END_PATH;
            PG -1 -2;
        ENDIF;

        evaluate_spectrometer;
        objective := focal_plane_tilt;
    ENDFIT 1e-10 100 optimizer objective;

    IF output_mode=1;
        out := 6;
    ELSEIF output_mode#1;
        out := 0;
    ENDIF;

    WRITE out 'p_bend_radius' p_bend_radius;
    WRITE out 'p_bend_angle' p_bend_angle;
    WRITE out 'p_drift_post_aperture' p_drift_post_aperture;
    WRITE out 'p_drift_pre_bend' p_drift_pre_bend;
    WRITE out 'p_drift_m5a_m5b' p_drift_m5a_m5b;
    WRITE out 'p_drift_post_bend' p_drift_post_bend;
    WRITE out 'p_drift_pre_hodoscope' p_drift_pre_hodoscope;
    WRITE out 'p_m5a_length' p_m5a_length;
    WRITE out 'p_m5a_quad' p_m5a_quad;
    WRITE out 'p_m5a_hex' p_m5a_hex;
    WRITE out 'p_m5b_length' p_m5b_length;
    WRITE out 'p_m5b_quad' p_m5b_quad;
    WRITE out 'p_m5b_hex' p_m5b_hex;
    WRITE out 'p_shape_in_1' p_shape_in_1;
    WRITE out 'p_shape_in_2' p_shape_in_2;
    WRITE out 'p_shape_in_3' p_shape_in_3;
    WRITE out 'p_shape_out_1' p_shape_out_1;
    WRITE out 'p_shape_out_2' p_shape_out_2;
    WRITE out 'p_shape_out_3' p_shape_out_3;
    WRITE out 'p_m5c_length' p_m5c_length;
    WRITE out 'p_m5c_quad' p_m5c_quad;
    WRITE out 'p_m5c_hex' p_m5c_hex;

    { ---- final spectrometer ---- }

    UNITY_MAP;
    CLEAR_RAYS;
    gen_rays 0 5;
    gen_rays -0.1 3;
    gen_rays +0.1 2;

    IF output_mode#0;
        PTY y_scale;
        DRAW_BEGIN_PATH;
    ENDIF;

    elt_spectrometer;

    IF output_mode=1;
        { output to GUI }
        DRAW_END_PATH;
        PG -1 -2;
    ENDIF;
    IF output_mode=2;
        { output to PDF file }
        DRAW_END_PATH;
        PG -12 -12;
    ENDIF;

    { ---- primary outputs ---- }

    evaluate_spectrometer;

    WRITE out '';

    WRITE out 'magnification:' ME(1,1) 'x';
    WRITE out 'focus:' ME(1,2)*100 'cm/rad';
    WRITE out 'dispersion:' ME(1,6)/central_energy*100 'cm/MeV';
    WRITE out 'resolution (algebraic):' resolution*central_energy*1000 'keV';
    WRITE out 'focal plane tilt:' focal_plane_tilt*180/PI 'deg';
    WRITE out 'focal plane bend:' focal_plane_bend '/m';
    WRITE out 'focal plane length:' focal_plane_length*100 'cm';
    WRITE out 'focal plane height:' focal_plane_height*100 'cm';
    
    WRITE out '';

    WRITE_MAP out;

    WRITE out '';

ENDPROCEDURE;

main;

ENDPROCEDURE;
mergs_ion_optics;
END;
